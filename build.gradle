plugins {
  // derive version from git tags, not persisted in properties
  id 'org.ajoberstar.reckon' version '0.12.0'
  // differentiate transient from implementation dependencies
  id 'java-library'
  // manage JavaFx modules
  id 'org.openjfx.javafxplugin' version '0.0.8'
  // publishing plugins
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'com.jfrog.artifactory' version '4.11.0'
  // encrypted credentials for bintray etc.
  id 'nu.studer.credentials' version '1.0.7'
}

reckon {
  scopeFromProp()
  stageFromProp('beta', 'rc', 'final')
}

group = 'de.example'
description = 'An example java library for experimental purposes'

ext {
  homepage = "${githubOrg}.github.com/$githubRepo"
  vcsUrl = "https://github.com/${githubOrg}/${githubRepo}"
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
  withJavadocJar()
  withSourcesJar()
}

javafx {
  modules = [ 'javafx.controls', 'javafx.graphics' ]
  version = '11'
}

repositories {
    jcenter()
}

dependencies {
    api 'io.reactivex.rxjava2:rxjava:2.2.4'
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true
    }
}

publishing {
  publications {
    simple(MavenPublication) {
      from components.java
      pom {
        name = "${project.group}:${project.name}"
        description = project.description
        url = homepage
        licenses {
          license {
            name = project.license_name
            url = project.license_url
          }
        }
        developers {
          developer {
            id = 'tamaracha'
            name = 'Tamara Cook'
            email = 'tamara.cook@anonym.com'
          }
        }
        scm {
            connection = "scm:git:git://github.com/${githubOrg}/${githubRepo}.git"
            developerConnection = "scm:git:ssh://github.com/${githubOrg}/${githubRepo}.git"
            url = vcsUrl
        }
      }
    }
  }
}

bintray {
  user = credentials.bintray_user
  key = credentials.bintray_api_key
  publications = ['simple']
  publish = true
  pkg {
    repo = project.bintrayRepo
    name = project.name
    desc = project.description
    licenses = [project.license_name]
    vcsUrl = vcsUrl
    version {
      name = version
      vcsTag = version
    }
  }
}
